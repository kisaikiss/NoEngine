#include "Particle.hlsli"
#include "../Random.hlsli"
RWStructuredBuffer<Particle> gParticles : register(u0);

RWStructuredBuffer<int> gFreeListIndex : register(u1);
RWStructuredBuffer<uint> gFreeList : register(u2);
struct PerFrame
{
    float time;
    float deltaTime;
    float2 pad;
};
ConstantBuffer<PerFrame> gPerFrame : register(b0);

static const int kMaxParticles = 1024;

[numthreads(1024, 1, 1)]
void main(uint3 DTid : SV_DispatchThreadID)
{
    uint particleIndex = DTid.x;
    if (particleIndex < kMaxParticles)
    {
        
        gParticles[particleIndex].translate += gParticles[particleIndex].velocity;
        gParticles[particleIndex].currentTime += gPerFrame.deltaTime;
        float alpha = 1.0f - (gParticles[particleIndex].currentTime / gParticles[particleIndex].lifeTime);
        gParticles[particleIndex].color.a = saturate(alpha);
        if (gParticles[particleIndex].color.a == 0)
        {
            gParticles[particleIndex].scale = float3(0.0f, 0.0f, 0.0f);
            int freeListIndex;
            InterlockedAdd(gFreeListIndex[0], 1, freeListIndex);
            
            // 最初のFreeListIndexの場所に死んだパーティクルのIndexを設定する。
            if ((freeListIndex + 1) < kMaxParticles)
            {
                gFreeList[freeListIndex + 1] = particleIndex;
            }
            else
            {
                // ここには来ないが、安全のため
                InterlockedAdd(gFreeListIndex[0], -1, freeListIndex);
            }
        }
    }
}